buildscript {
    ext.kotlin_version = '1.2.10'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.java.dev.jna:jna:4.5.0'
    }
}

apply plugin: 'kotlin'

version = "0.58.0-SNAPSHOT"
group = "ai.snips"

repositories {
    jcenter()
    maven {
        url "https://nexus-repository.snips.ai/repository/snips-maven-releases/"
    }
    maven {
        url "https://nexus-repository.snips.ai/repository/snips-maven-snapshots/"
    }
}

configurations {
    aarArchives
    jarArchives
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'net.java.dev.jna:jna:4.5.0'
    compile "ai.snips:snips-nlu-ontology:0.53.1"
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.36'
}

def buildType = project.hasProperty("debug") ? "debug" : "release"
println "Using build type $buildType"

def rustTargetPath = project.hasProperty("rustTargetPath") ? project.rustTargetPath : "../target"
println "Using rust target path $rustTargetPath"

def soForJar = [
        ["$rustTargetPath/$buildType/libsnips_nlu_ffi.so", "linux-x86-64", "linuxNative" ],
        ["$rustTargetPath/$buildType/libsnips_nlu_ffi.dylib", "darwin", "macOsNative" ],
        ["$rustTargetPath/arm-unknown-linux-gnueabihf/$buildType/libsnips_nlu_ffi.so", "linux-arm", "linuxArmCross"]
]

def jarClassifier = {
    def exisiting = soForJar.findAll{ file(it[0]).exists() }
    if(exisiting.size == 1) exisiting[0][1]
    else if(exisiting.size == 0) "naked"
    else "multiarch"
}

jar {
    classifier "${ -> jarClassifier() }"
}

def jniLibsDir = new File(buildDir, "jniLibs")

soForJar.forEach {
    def taskName = "copySo${it[2].capitalize()}ForJar"
    def soFile = file(it[0])
    def destDir = new File(jniLibsDir, it[1])

    task(taskName, type: Copy) {
        from soFile
        into destDir
    }
    processResources.dependsOn(taskName)

}

sourceSets {
    main {
        resources {
            srcDir jniLibsDir
        }
    }
}

def aarDir = new File(buildDir, "aar")

task("aar", type: Zip) {
    destinationDir new File(buildDir, "libs")
    baseName = "${project.name}-android"
    version = project.version
    extension = "aar"
    from aarDir
}

task("classesJarForAar", type: Zip) {
    destinationDir aarDir
    archiveName "classes.jar"
    from new File(buildDir, "classes/java/main")
}

classesJarForAar.dependsOn(classes)
aar.dependsOn(classesJarForAar)

task("manifestForAar", type: Copy) {
    from new File("src/main/android/AndroidManifest.xml")
    destinationDir aarDir
}

aar.dependsOn(manifestForAar)

def soForAar = [
        ["$rustTargetPath/arm-linux-androideabi/$buildType/libsnips_nlu.so", "armeabi"],
        ["$rustTargetPath/armv7-linux-androideabi/$buildType/libsnips_nlu.so", "armeabi-v7a"],
        ["$rustTargetPath/aarch64-linux-android/$buildType/libsnips_nlu.so", "arm64-v8a"],
        ["$rustTargetPath/i686-linux-android/$buildType/libsnips_nlu.so", "x86"],
        ["$rustTargetPath/x86_64-linux-android/$buildType/libsnips_nlu.so", "x86_64"]
]

soForAar.forEach {
    def taskName = "copySo${it[1].capitalize()}ForAar"
    def soFile = file(it[0])
    def destDir = new File(aarDir, "jni/${it[1]}")

    task(taskName, type: Copy) {
        from soFile
        into destDir
    }
    aar.dependsOn(taskName)

}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

artifacts {
    aarArchives aar.archivePath
    jarArchives jar.archivePath
}

apply plugin: 'maven'

def _nexusUsername = project.hasProperty("nexusUsername") ? nexusUsername : ""
def _nexusPassword = project.hasProperty("nexusPassword") ? nexusPassword : ""

uploadJarArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://nexus-repository.snips.ai/repository/snips-maven-releases/") {
                authentication(userName: _nexusUsername, password: _nexusPassword)
            }
            snapshotRepository(url: "https://nexus-repository.snips.ai/repository/snips-maven-snapshots/") {
                authentication(userName: _nexusUsername, password: _nexusPassword)
            }
        }
    }
}

uploadJarArchives.dependsOn(jar)
uploadArchives.dependsOn(uploadJarArchives)

uploadAarArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://nexus-repository.snips.ai/repository/snips-maven-releases/") {
                authentication(userName: _nexusUsername, password: _nexusPassword)
            }
            snapshotRepository(url: "https://nexus-repository.snips.ai/repository/snips-maven-snapshots/") {
                authentication(userName: _nexusUsername, password: _nexusPassword)
            }

            repository(url: "file://localhost/tmp/myRepo/")
            pom.name = "snips-nlu-android"

            pom.whenConfigured { pom ->
                pom.dependencies.find { dep -> dep.groupId == 'net.java.dev.jna' && dep.artifactId == 'jna' }.scope = "provided"
            }
        }
    }
}

uploadAarArchives.dependsOn(aar)
uploadArchives.dependsOn(uploadAarArchives)
